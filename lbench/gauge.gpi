stats ARG1 u 2 name "h2p" nooutput
stats ARG1 u 3 name "p2h" nooutput
stats ARG1 u 4 name "p2p" nooutput
stats "gauge_1-1_cc.dat" u 2 name "cc" nooutput

set title ARG2

set logscale x 2
set style data lp
#set yrange [0:*]

set xlabel "Workload intensity (flops/element)"
set ylabel "Measured interference coefficient"
set y2label "UPI traffic from PCM (GB/s)"

set y2range [0:160]

set grid

$pcm << EOD
# threads=12
1 88831.2
2 87215.2
4 86411.4
8 83153.7
16 55735.6
32 19906
64 6472.96
EOD

set y2tics

plot ARG1 u 1:($2/h2p_min) t "iBench host-to-pool", "" u 1:($3/p2h_min) t "iBench pool-to-host" , "" u 1:($4/p2p_min) t "iBench pool-to-pool", "gauge_1-1_cc.dat" u 1:($2/cc_min) t "iBench host-to-pool (HT)", $pcm u 1:($2/1000) t "UPI traffic (right axis)" dt 2 axes x1y2
